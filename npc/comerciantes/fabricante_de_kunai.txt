//===== rAthena Script =======================================
//= Kunai Merchant Kashin
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

que_ng,72,29,3	script	Kunai Merchant Kashin	83,{
	if(BaseClass == Job_Ninja) {
		mes "[Kashin]";
		mes "Eu sou Kashin, o distributor de Kunais para Ninjas.";
		mes "Dê uma olhada e veja está interessado em quaisquer de minhas mercadorias.";
		next;
		switch(select("Kunai Veneno Mortal","Kunai Nevasca","Kunai Sopro do Vento","Kunai Terra Negra","Kunai Onda de Calor","Cancelar")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "Bem então, obrigado por visitar minha loja.";
			mes "Por favor, volte quando precisar de algumas Kunais.";
			mes "Até breve.";
			close;
		}
	}
	mes "[Kashin]";
	mes "Eu sou Kashin, o distributor de Kunais para Ninjas.";
	mes "Se você possui algum amigo da Ninja conte a ele sobre mim peça que venha falar comigo.";
	close;
}

function	script	Kunai_Trade	{
	setarray .@item_req[0], getarg(0), getarg(2);
	setarray .@req_amount[0], getarg(1), getarg(3);
	.@item_id = getarg(4);

	mes "[Kashin]";
	mes "Você pode trocar";
	mes .@req_amount[0] +" "+ getitemname(.@item_req[0]) +" e";
	mes .@req_amount[1] +" "+ getitemname(.@item_req[1]) +" para cada";
	mes "um conjunto de 10 "+ getitemname(.@item_id) +".";
	next;
	mes "[Kashin]";
	mes "Eu posso só lhe dar de cada vez um máximo de 500 Kunais.";
	mes "Se quiser cancelar a troca basta que digite 0.";
	mes "Quantas Kunais você gostaria?";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[Kashin]";
		mes "Ahn? Eu sinto muito, mas eu não o posso dar uma quantidade acima de 500 unidades.";
		close;
	}
	if (countitem(.@item_req[0]) < .@amount*.@req_amount[0] || countitem(.@item_req[1]) < .@amount*.@req_amount[1]) {
		mes "[Kashin]";
		mes "Parece que você não tem a quantidade de itens necessários para a troca.";
		mes "Por favor, verifique seus itens e tente novamente.";
		close;
 	}
	if (checkweight(.@item_id, .@amount*10) == 0) {
		mes "[Kashin]";
		mes "Parece que não vai poder carregar todas os itens.";
		mes "Por favor, verifique a quantidade de peso que carrega e tente novamente.";
		close;
	}
	mes "[Kashin]";
	mes "Ótimo, tudo está em ordem, assim prossigamos com a troca.";
	mes "Espero que faça bom proveito com suas novas Kunais.";
	delitem .@item_req[0],.@req_amount[0]*.@amount;
	delitem .@item_req[1],.@req_amount[1]*.@amount;
	getitem .@item_id,10*.@amount;
	close;
}
