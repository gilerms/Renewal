//===== rAthena Script ======================================= 
//= Izlude Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Izlude.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Updated to match the new Izlude Map. [Masao]
//= 1.2 Added Renewal Izlude viewpoint coordinates.
//= 1.3 Navigation system update. [Euphy]
//=     Optimized.
//= 1.4 Added Izlude duplicates. [Euphy]
//============================================================ 

-	script	::Guia de Izlude	-1,{
	cutin "prt_soldier",2;
	mes "[Guia de Izlude]";
	mes "Bem-vindo a ^8B4513Izlude^000000,";
	mes "cidade-satélite de Prontera.";
	mes "Você precisa de ajuda para navegar na cidade?";
	F_Navi("[Guia de Izlude]");
	next;
	set .@map$, strnpcinfo(4);
	while (1) {
		switch(select("Instalações Principais:Comerciantes e auxiliares:Remover marcas do minimapa:Cancelar")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[Academia Real]:[Porto]:[Dirigível Internacional]:[Arena]:[Loja de Kit]:[Guilda de Espadachim]:[Associação de Encantamento]:[Forja]:Menu anterior")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "Aventureiros novatos devem ir para "+F_Navi("Academia Real","iz_ac01,100,39")+" para receber mais informações e treinamentos!";
					mes "Você gostaria de checar outros locais?";
					viewpoint 1,128,260,0,0xD2691E;
					next;
					break;
				case 2:
					mes "[Guia de Izlude]";
					mes "Você pode usar o "+F_Navi("Porto",.@map$+",197,205","^B9062F");
					mes "para ir para ^8B4513Alberta^000000 ou a ^8B4513ilha de Byalan^000000.";
					mes "Cuidado com o ^8B4513Calabouço de Izlude^000000";
					mes "na Ilha Byalan desde que é";
					mes "cheio de monstros terríveis.";
					mes "Você gostaria de checar outros locais?";
					viewpoint 1,197,205,1,0xFF0000;
					next;
					break;
				case 3:
					mes "[Guia de Izlude]";
					mes "O "+F_Navi("Dirigível Internacional",.@map$+",202,75","^0000FF");
					mes "vai para ^8B4513Juno^000000 e ^8B4513Rachel^000000.";
					mes "Deixe-me marcar sua localização";
					mes "no seu mini-mapa.";
					mes "Você gostaria de checar outros locais?";
					viewpoint 1,210,73,2,0x0A82FF;
					next;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "O "+F_Navi("Arena",.@map$+",213,161","^FF5400");
					mes "é um lugar perfeito para";
					mes "teste suas capacidades de combate.";
					mes "Deixe-me marcar sua localização";
					mes "no seu mini-mapa.";
					mes "Você gostaria de checar outros locais?";
					viewpoint 1,213,161,3,0xFF5400;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Loja de Kit",.@map$+",110,182","^006400");
					viewpoint 1,108,179,4,0xAAFF00;
					next;
					break;
				case 6:
					mes "[Guia de Izlude]";
					mes "O "+F_Navi("Guilda do Espadachim",.@map$+",51,172")+" é onde você pode ^800080alterar sua classe para o Espadachim.^000000";
					mes "Deixe-me marcar sua localização";
					mes "no seu mini-mapa.";
					mes "Você gostaria de checar outros locais?";
					viewpoint 1,52,172,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("Associação de Encantamento",.@map$+",97,125","^B9062F");
					viewpoint 1,97,125,6,0xDA70D6;
					next;
					break;
				case 8:
					callsub L_Mark, F_Navi("Forja",.@map$+",153,126");
					viewpoint 1,153,126,7,0xAAFF00;
					next;
					break;
				case 9:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[Oficial de Teleporte do Éden]:[Funcionária da Kafra]:[Hipnotista]:[Ajudante de Recém-Casados]:[Caçador de Guilda de Mercenários]:[Aparador de Animais de Estimação]:Menu Anterior")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "O "+F_Navi("Oficial do Teleporte de Eden",.@map$+",131,148","^0000FF");
					mes "está atualmente dentro";
					mes "do prédio da Guilda dos Espadachins.";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Você gostaria de checar outros locais?";
					viewpoint 1,131,148,8,0x0A82FF;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("Funcionária da Kafra",.@map$+",128,148","^FF5400"),1;
					viewpoint 1,128,148,9,0xFF5400;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("Hipnotizador",.@map$+",127,175"),1;
					viewpoint 1,127,175,10,0x006400;
					next;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "Se você quiser ir em lua de mel para";
					mes "^8B4513Jawaii^000000, o paraíso dos recém-casados";
					mes "por favor fale com o";
					mes F_Navi("Ajudante de Casamento",.@map$+",180,224","^B9062F")+".";
					mes "Você gostaria de checar outros locais?";
					viewpoint 1,180,224,11,0xFF0000;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Guilda dos Mercenários",.@map$+",47,170","^0000FF");
					viewpoint 1,47,170,12,0xDA70D6;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Pet Groomer","izlude_in,72,98"),1;
					viewpoint 1,108,179,13,0x006400;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Guia de Izlude]";
			mes "Claro, eu removerei todas as marcas do seu mini-mapa.";
			mes "Há mais alguma coisa que eu possa fazer por você?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Guia de Izlude]";
			mes "Aproveite a sua estadia.";
			mes "Até breve~!";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Guia de Izlude]";
	if (getarg(1,0)) {
		mes "Deixe-me marcar a localização do";
		mes getarg(0);
	} else
		mes "Deixe-me marcar a localização do" + getarg (0);
		mes "no seu mini-mapa.";
		mes "Você gostaria de checar outros locais?";
	return;
}

izlude,129,175,4	duplicate(Guia de Izlude)	Guia#01izlude	105
izlude_a,129,175,4	duplicate(Guia de Izlude)	Guia#01izlude_a	105
izlude_b,129,175,4	duplicate(Guia de Izlude)	Guia#01izlude_b	105
izlude_c,129,175,4	duplicate(Guia de Izlude)	Guia#01izlude_c	105
izlude_d,129,175,4	duplicate(Guia de Izlude)	Guia#01izlude_d	105

izlude,133,113,4	duplicate(Guia de Izlude)	Guia#02izlude	105
izlude_a,133,113,4	duplicate(Guia de Izlude)	Guia#02izlude_a	105
izlude_b,133,113,4	duplicate(Guia de Izlude)	Guia#02izlude_b	105
izlude_c,133,113,4	duplicate(Guia de Izlude)	Guia#02izlude_c	105
izlude_d,133,113,4	duplicate(Guia de Izlude)	Guia#02izlude_d	105

//izlude,120,206,4	duplicate(Guia de Izlude)	Guia#03izlude	105
