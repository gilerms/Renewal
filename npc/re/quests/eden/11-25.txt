//===== rAthena Script =======================================
//= Eden Group Quests - Mission [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Bug fixes. [Capuche]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//= 2.0 Parcialmente traduzido. [Gilerms]
//============================================================

moc_para01,36,38,3	script	Missão [11 - 25]#Tuto	4_BOARD3,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- Você precisa ter um -";
		mes "- ^4d4dff'Emblema do Éden'^000000 -";
		mes "- para conseguir receber missões. -";
		close;
	}
	for ( .@i = 11124; .@i < 11134; .@i++ )
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes " - Você deve coletar sua - ";
			mes " - recompensa antes de começar - ";
			mes " - uma nova missão. - ";
			mes " ";
			mes " - Gerente : Spike - ";
			close;
		}
		else if (.@j >= 0) {
			mes " - Você só pode solicitar - ";
			mes " - uma missão de cada vez. - ";
			mes " ";
			mes " - Gerente : Spike - ";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes " - Não há missões - ";
		mes " - disponíveis para você no momento. - ";
		close;
	}
	if (BaseLevel > 25) {
		mes " - Essas missões são muito - ";
		mes " - fáceis para você. - ";
		mes " - Procure por missões - ";
		mes " - apropriadas para seu nível. - ";
		close;
	}
	mes " - Quadro de Avisos da Missão -";
	mes " - para iniciantes. -";
	mes " ";
	mes " - Gerente : Spike -";
	next;
	switch(select("Caçar Zangões:Caçar Condores:Perna de Gafanhoto:Caçar Rabo de Verme:Caçar Esporos:Controle de pragas:Caçar Mukas:Caçar Familiares:Coletar Penas:Coletar Esporos Venenosos:Cancelar")) {
		case 1: callsub L_Quest,11114,"Hornet Hunting"," - Caçar 10 ^4d4dff'Zangões'^000000 - "," - Noroeste de Prontera. - ";
		case 2: callsub L_Quest,11115,"Condor Hunting"," - Caçar 20 ^4d4dff'Condores'^000000 - "," - Sul de Morroc. - ";
		case 3: callsub L_Quest,11116,"Grasshopper's Leg"," - Caçar 10 ^4d4dff'Rockers'^000000 - "," - Sudoeste de Prontera e - "," - trazer 10 ^4d4dffPernas de Gafanhoto^000000. - ";
		case 4: callsub L_Quest,11117,"Worm Tail Hunting"," - Caçar 20 ^4d4dff'Rabo de Verme'^000000 - "," - Sudeste de Payon.- ";
		case 5: callsub L_Quest,11118,"Spore Hunting"," - Caçar 30 ^4d4dff'Esporos'^000000 - "," - Arredores de Payon. - ";
		case 6: callsub L_Quest,11119,"Pest Control"," - Caçar 20 ^4d4dffOvos de Besouro ladrão^000000 - "," - No esgoto de Prontera e - "," - trazer 10 ^4d4dffCrisálidas^000000. - ";
		case 7: callsub L_Quest,11120,"Muka Hunting"," - Caçar 20 ^4d4dff'Mukas'^000000 - "," - Sul de Prontera. - ";
		case 8: callsub L_Quest,11121,"Familiar Hunting"," - Caçar 20 ^4d4dffFamiliares^000000 - "," - in the Culvert Sewers - "," - Oeste de Prontera. - ";
		case 9: callsub L_Quest,11122,"Collect Feathers"," - Caçar 30 ^4d4dff'Peco-Pecos'^000000 - "," - Sul de Prontera. - ";
		case 10: callsub L_Quest,11123,"Collect Poison Spores"," - Caçar 30 ^4d4dffEsporos Venenosos^000000 - "," - Dentro da Torre de Geffen e - "," - trazer 5 ^4d4dffEsporos Venenosos^000000. - ";
		case 11:
			mes " - Pare de ler o quadro de avisos. - ";
			close;
	}

L_Quest:
	.@quest_status = checkquest(getarg(0)+10,PLAYTIME);
	if (.@quest_status == 0 || .@quest_status == 1) {
		mes " - Missão ^4d4dff"+ getarg(1) +"^000000 - ";
		mes " - já foi completada hoje. - ";
		mes " ";
		mes " - Gerente : Spike - ";
		close;
	}
	mes " - Missão '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	mes " ";
	mes " - Gerente : Spike - ";
	next;
	if (select("Vou fazer essa missão.:Procurar outras missões.") == 2) {
		mes " - Vamos procurar por -";
		mes " - outras missões. - ";
		close;
	}
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	Spike	4_F_CHILD,{
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			switch(.@i) {
				case 11114: callsub L_Quest,.@i,"Hornet Hunting",900;
				case 11115: callsub L_Quest,.@i,"Condor Hunting",1800;
				case 11116: callsub L_Quest,.@i,"Grasshopper's Leg",1400,940,10;
				case 11117: callsub L_Quest,.@i,"Worm Tail Hunting",2600;
				case 11118: callsub L_Quest,.@i,"Spore Hunting",3900;
				case 11119: callsub L_Quest,.@i,"Extirpate Insects",2900,915,10;
				case 11120: callsub L_Quest,.@i,"Muka Hunting",3200;
				case 11121: callsub L_Quest,.@i,"Familiar Hunting",3200;
				case 11122: callsub L_Quest,.@i,"Collect Feathers",5400;
				case 11123: callsub L_Quest,.@i,"Collect Poison Spores",6000,7033,5;
			}
		}
		else if (.@j >= 0) {
			mes "[Spike]";
			mes "Você ainda não terminou sua missão.";
			mes "Mantenha o bom trabalho. Você está quase pronto.";
			close;
		}
	}
	mes "[Spike]";
	mes "Você está procurando emprego?";
	mes "Nós sempre temos muito trabalho a fazer.";
	next;
	mes "[Spike]";
	mes "Por que você não encontra alguma tarefa interessante?";
	close;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id,HUNTING) == 2) {
		.@item_req = getarg(3,0);
		.@req_amount = getarg(4,0);
		if (.@item_req) {
			if (countitem(.@item_req) < .@req_amount) {
				mes "[Spike]";
				mes "Você não tem "+ getitemname(.@item_req) +" suficiente.";
				mes "Você precisa trazer "+ .@req_amount +" "+ getitemname(.@item_req) +".";
				close;
			}
		}
		mes "[Spike]";
		mes "Hmm... Deixe-me ver~";
		mes "Você completou a missão '"+ getarg(1) +"'.";
		next;
		mes "[Spike]";
		mes "Bom trabalho~!!";
		mes "Nosso cliente está tão feliz com o seu trabalho. Ele irá apreciar o que você fez.";
		mes "Por favor, nos ajude novamente. Obrigado!";
		if (.@item_req)
			delitem .@item_req,.@req_amount;
		erasequest .@quest_id;
		setquest .@quest_id+10;
		getexp getarg(2),0;
		close;
	}
}
