//===== rAthena Script ======================================= 
//= Clothes Dyer
//===== By: ================================================== 
//= Usnul
//===== Current Version: ===================================== 
//= 2.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Clothes dyer with standard palletes
//===== Additional Comments: ================================= 
//= Fully working
//= 2.1 Shortened some labels to make this script loading,
//= Added Black+White Colors desc [Lupus]
//= 2.1a added adv classes + baby class support [Lupus]
//= 2.2 Spell checked. [Nexon]
//= 2.3 Fixed dis $hit! [Poki#3]
//= 2.4 Disabled cloth dyeing since it's not implemented
//	   on official servers and cause your client to crash
//	   [Playtester]
//= 2.5 Optimized. [Euphy]
//============================================================

prt_in,284,168,2	script	Tintureiro	55,{
function Dyes;

	set .@EnableDyes,1;	// Disable (set to 0) if palettes cause errors [see note v2.4]

	set .n$,"[Tintureiro]";
	mes .n$;
	mes "11... 12... Mmm... Boa. Acho que vou conseguir terminar antes da festa de hoje. Oh! Eu não percebi que você estava aqui!";
	while(1) {
		next;
		mes .n$;
		mes "How may I assist you?";
		next;
		switch(select("- Talk:- Dye Clothing:- Price List:- ^777777Cancel^000000")) {
		case 1:
			mes .n$;
			mes "A vida pode ter ficado um pouco melhor, mas quando eu olho para todas as ordens que recebi ... Ufa!";
			mes "Parece que as jovens de Rune Midgard devem estar muito bem nestes dias.";
			next;
			mes .n$;
			mes "Não que eu ache que eles estão se entregando a qualquer coisa luxuosa, veja bem.";
			mes "Eu não acho que há algo errado com a busca da beleza e estar na moda.";
			next;
			mes .n$;
			mes "Para nós humanos, que não têm pele colorida ou escamas decorativas, as roupas são uma das poucas maneiras que temos para mostrar nossa personalidade, estilo e beleza.";
			next;
			mes .n$;
			mes "Haha ... Quando penso nisso, realmente sinto que meu trabalho vale a pena. Acredito que ofereço um serviço que as pessoas desejam.";
			next;
			mes .n$;
			mes "Você pode sentir isso também? A energia que é liberada quando tecido áspero e couro são trazidos à vida com cor? .....";
			next;
			mes .n$;
			mes "Embora o processo seja muito tedioso e demorado, a alegria e a felicidade que sinto quando um vestido termina, compensa todo o trabalho duro !!";
			break;
		case 2:
			mes .n$;
			if (!.@EnableDyes) {
				mes "Desculpe, estou muito ocupado com outros trabalhos para pintar suas roupas.";
				break;
			}
			mes "Oh ... você precisa do meu trabalho? Bem ... tudo bem, parece bom.";
			next;
			mes .n$;
			mes "Por favor, escolha uma cor";
			mes "que combina com você.";
			next;

			// Dyes(<color value>,<dye number>{,...});
			// Colors: [1]Orange, [2]Violet, [3]Red, [4]Black, [5]Green, [6]Blue, [7]White, [8]Yellow
			switch(BaseJob) {
			case Job_Novice:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(6,1,3,2,5,3,4,4);
			case Job_Swordman:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(3,1,5,3,4,4);
			case Job_Mage:
				if (Sex == SEX_MALE) Dyes(3,1,2,2,1,3,7,4); else Dyes(3,1,2,2,7,3,4,4);
			case Job_Archer:
				if (Sex == SEX_MALE) Dyes(4,1,2,2,5,3); else Dyes(3,1,5,2,7,3,4,4);
			case Job_Acolyte:
				if (Sex == SEX_MALE) Dyes(6,1,3,2,7,3,4,4); else Dyes(3,1,5,3,4,4);
			case Job_Merchant:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(2,1,5,3,4,4);
			case Job_Thief:
				if (Sex == SEX_MALE) Dyes(5,1,3,2,7,4); else Dyes(3,1,7,3,4,4);
			case Job_Knight:
			case Job_Crusader:
				if (Sex == SEX_MALE) Dyes(2,1,3,3,4,4); else Dyes(6,1,7,3,4,4);
			case Job_Priest:
			case Job_Monk:
				if (Sex == SEX_MALE) Dyes(6,1,5,2,3,3,7,4); else Dyes(3,1,5,2,7,3,4,4);
			case Job_Wizard:
			case Job_Sage:
				if (Sex == SEX_MALE) Dyes(6,1,5,3,4,4); else Dyes(3,1,6,2,7,3,5,4);
			case Job_Blacksmith:
			case Job_Alchemist:
				if (Sex == SEX_MALE) Dyes(4,4,5,2,7,3); else Dyes(3,1,5,2,2,3,4,4);
			case Job_Hunter:
			case Job_Bard:
			case Job_Dancer:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(6,1,5,3,2,4);
			case Job_Assassin:
			case Job_Rogue:
				if (Sex == SEX_MALE) Dyes(6,1); else Dyes(4,1,8,2,7,3);
			case Job_Super_Novice:
				if (Sex == SEX_MALE) Dyes(2,1,6,2,5,3,4,4); else Dyes(6,1,3,2,5,3,4,4);
			default: 
				mes "Uau, eu nunca vi roupas assim antes! Me desculpe, mas eu não acho que posso pintá-lo.";
				close;
			}
		case 3:
			mes .n$;
			mes "Cada cor custa 10000z e 1 corante:";
			mes " ";
			mes " - ^FF4422Vermelha^000000: Corante Escarlate";
			mes " - ^D5A500Amarela^000000: Corante Limão";
			mes " - ^AA00AAVioleta^000000: Corante Violeta";
			mes " - ^FF8800Laranja^000000: Corante Laranja";
			mes " - ^4422FFAzul^000000: Corante Azul Cobalto";
			mes " - ^009500Verde^000000: Corante Verde Escuro";
			mes " - Preto: Corante Preto";
			mes " - Branco: Corante Branco";
			break;
		case 4:
			goto L_End;
		}
	}

L_End:
	mes .n$;
	mes "Sinta-se em casa. Mesmo que eu não tenha tempo para você.";
	close;

function Dyes {
	setarray .@DyeName$[0],"a cor padrão","Laranja","Violeta","Vermelho","Preto","Verde","Azul","Branco","Amarelo";
	setarray .@DyeItems[0],0,980,981,975,983,979,978,982,976;
	set .@menu$,"- Default:";
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+2)
		set .@menu$, .@menu$+"- "+.@DyeName$[getarg(.@i)]+":";
	set .@menu$, .@menu$+"- ^777777Cancelar^000000";
	set .@s, select(.@menu$)-1;
	if (.@s == (getargcount()/2)+1) goto L_End;
	mes .n$;
	if (.@s == 0) {
		mes "Eu posso mudar seu corante para";
		mes "o padrão de graça.";
		mes "Tem certeza?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			mes .n$;
			setlook 7,0;
			mes "Suas roupas foram tingidas "+.@DyeName$[0]+".";
			close;
		case 2:
			goto L_End;
		}
	}
	if (!countitem(.@DyeItems[getarg((.@s-1)*2)])) {
		mes "Para eu pintar suas roupas, eu precisarei dos corantes apropriados. Por favor, verifique minha lista de preços para a informação.";
		close;
	}
	if (Zeny < 10000) {
		mes "Sinto muito, mas você não tem dinheiro suficiente.";
		close;
	}
	delitem .@DyeItems[getarg((.@s-1)*2)],1;
	set Zeny, Zeny-10000;
	setlook 7, ((.@s)?(getarg((.@s-1)*2+1)):0);
	mes "Suas roupas foram tingidas "+((.@s)?.@DyeName$[getarg((.@s-1)*2)]:.@DyeName$[0])+".";
	close;
}
}
