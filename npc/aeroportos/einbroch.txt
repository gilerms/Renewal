//===== LivedBR Script ======================================= 
//= Aeroporto de Einbroch
//===== Por: ================================================= 
//= rAthena Dev Team
//===== Versão Atual: ========================================
//= 1.1
//===== Compatível : ========================================= 
//= rAthena / LivedBR
//===== Descrição : ========================================== 
//= Equipe do Aeroporto de Einbroch.
//===== Additional Comments: ================================= 
//= 1.0 Primeira Versão.
//= 1.1 Tradução pt_br. [Vrods]
//============================================================ 

airport,143,43,5	script	Equipe do Aeroporto#airport1a::airport1	90,{
	mes "[Equipe do Aeroporto]";
	mes "Bem vind"+(sex?"o":"a")+" ao";
	mes "Aeroporto de Einbroch";
	mes "onde oferecemos";
	mes "vôos para as cidades de";
	mes "Juno, Lighthalzen e Hugel.";
	next;
	if (select("Embarcar no Dirigível.:Cancelar.") == 1) {
		mes "[Equipe do Aeroporto]";
		mes "A taxa de embarque";
		mes "é 1.200 zeny, mas se você tiver";
		mes "um Bilhete Grátis para Aeroplano,";
		mes "a taxa não será cobrada.";
		mes "e voce poderá embarcar no dirigível.";
		next;
		if (select("Sim:Não") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1;
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Equipe do Aeroporto]";
			mes "Sinto muito, mas você não";
			mes "tem um Bilhete para Aeroplano";
			mes "e nem zeny suficiente para";
			mes "o embarque. Lembre-se, a";
			mes "taxa de embarque é 1.200 zenys.";
			close;
		}
	}
	mes "[Equipe do Aeroporto]";
	mes "Obrigado e";
	mes "tenha um bom dia.";
	close;
}

airport,158,43,5	duplicate(airport1)	Equipe do Aeroporto#airport1b	90
airport,126,43,5	duplicate(airport1)	Equipe do Aeroporto#airport1c	90

airport,143,49,3	script	Equipe de Embarque#airport2a::airport2	90,{
	mes "[Equipe de Recepção]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao Aeroporto de Einbroch.";
	mes "Se você está chegando do seu";
	mes "vôo, deixe-me guia-lo até o";
	mes "terminal principal. Caso contrário, por favor embarque no Aeroplano para ir a";
	mes "Juno, Lighthalzen ou Hugel.";
	next;
	if (select("Deixar o terminal principal:Cancelar") == 1) {
		mes "[Equipe de Recepção]";
		mes "Quando você estiver no terminal principal, você irá precisar pagar a taxa novamente";
		mes "para embarcar em um dirigível. Você só deverá";
		mes "sair da cidade de Einbroch";
		mes "se esse for seu destino pretendido se não";
		mes "Prossiga para o terminal principal.";
		next;
		if (select("Sim:Não") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Equipe de Recepção]";
	mes "Certo, obrigado";
	mes "pelo seu apoio e";
	mes "espero que você tenha";
	mes "um voo agradável.";
	close;
}

airport,126,51,3	duplicate(airport2)	Equipe de Embarque#airport2b	90
airport,158,50,3	duplicate(airport2)	Equipe de Embarque#airport2c	90

einbroch,94,267,3	script	Equipe do Aeroporto#ein01	91,{
	mes "[Equipe do Aeroporto]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao";
	mes "Aeroporto de Einbroch.";
	mes "Utilize essa porta para";
	mes "embarcar no digirível que";
	mes "irá para Juno, Lighthalzen e";
	mes "Hugel na República de Schwarzwald.";
	next;
	mes "[Equipe do Aeroporto]";
	mes "Caso Einbroch seja o seu destino,";
	mes "desça as escadas e fale com";
	mes "nossa Equipe de Recepção";
	mes "para levá-lo até o";
	mes "terminal principal. Obrigado";
	mes "e volte quando quiser.";
	close;
}
