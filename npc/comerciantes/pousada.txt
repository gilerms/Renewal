//===== rAthena Script =======================================
//= Inn Npcs
//===== By: ==================================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: =====================================
//= 2.9a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Inn Npcs, Save and Heal
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: =================================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=     and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//= 2.9 Added switch menus, updated to match AEGIS scripts. [Kisuka]
//= 2.9a Added 'npcskill' command. [Euphy]
//============================================================


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	Funcionária da Pousada#Sammy	53,{

	callfunc "F_InnMaid","[Funcionária Sammy]","Nenkaras","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	Funcionária da Pousada#Ahlma	53,{

	callfunc "F_InnMaid","[Funcionária Ahlma]","Nenkaras","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
alberta_in,32,142,3	script	Funcionária da Pousada#Jennie	53,{

	callfunc "F_InnMaid","[Funcionária Jennie]","'Pousada do Pescador'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	Funcionária da Pousada#Cena	53,{

	callfunc "F_InnMaid","[Funcionária Cena]","'Ifrit,' a única pousada na cidade de Geffen.","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	Funcionária da Pousada#Ahee	53,{

	callfunc "F_InnMaid","[Funcionária Ahee]","Pousada de Payon","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	Funcionária da Pousada#Hasna	53,{

	callfunc "F_InnMaid","[Funcionária Hasna]","Pousada de Morroc","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	Funcionária da Pousada#Manar	53,{

	callfunc "F_InnMaid","[Funcionária Manar]","Pousada de Morroc","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	Inn Maid#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","Pousada de Al De Baran","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Inn Keeper#Annie	931,{

	callfunc "F_InnMaid","[Annie]","Pousada de Rachel","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	Funcionária do Hotel#01	86,{
	mes "[Funcionário do Hotel]";
	mes "Bem vindo ao";
	mes "o Dragão Real";
	mes "onde você pode encontrar o";
	mes "melhores acomodações";
	mes "e o melhor serviço.";
	next;
	switch(select("Salvar ponto:Custo - 5.000 zeny:Cancelar")) {
	case 1:
		savepoint "lhz_in02",209,275,1,1;
		mes "[Funcionário do Hotel]";
		mes "Obrigado, seu";
		mes "Respawn Point tem";
		mes "foi salvo aqui em";
		mes "o Dragão Real.";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[Funcionário do Hotel]";
			mes "Sinto muito, mas";
			mes "você precisa de 5.000 zeny";
			mes "para fazer o check-in.";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[Funcionário do Hotel]";
		mes "Obrigado.";
		mes "Espero que você goste";
		mes "sua estadia no";
		mes "Dragão Real.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[Funcionário do Hotel]";
		mes "Obrigado e";
		mes "tenha um bom dia.";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	Pousada Maid#Receptionista	53,{

	callfunc "F_InnMaid","[Receptionista]","Pousada de Hugel","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	Mestre da Hospedaria#Receptionista	709,{
	mes "[Mestre da Hospedaria]";
	mes "Bom dia.";
	mes "Bem vindo ao";
	mes "mais confortável";
	mes "pousada aqui em veias.";
	next;
	switch(select("Salvar:Descansar -> 5000 zeny:Sair")) {
	case 1:
		mes "[Mestre da Hospedaria]";
		mes "Seu Ponto de Respawn";
		mes "foi salvo em Veins.";
		mes "Aproveite a sua estadia na cidade.";
		savepoint "ve_in",157,209,1,1;
		close;
	case 2:
		mes "[Mestre da Hospedaria]";
		if(Zeny < 5000){
			mes "Sinto muito, mas eu não";
			mes "acho que você tem o suficiente";
			mes "dinheiro para checar. O";
			mes "taxa de serviço é de 5.000 zenys.";
			close;
		}
		mes "Aproveite a sua estadia ~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[Mestre da Hospedaria]";
		mes "Por favor, venha novamente.";
		close;
	}
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	.@npc_name$ = getarg(0);

	mes .@npc_name$;
	mes "Bem-vindo ao";
	mes getarg(1) + ".";
	mes "Como posso ajudá-lo?";
	next;
	switch(select("Salvar:Descansar -> 5000 zeny:Cancelar")) {
	case 1:
		mes .@npc_name$;
		mes "Seu ponto de respawn";
		mes "foi salvo.";
		mes "Obrigado,";
		mes "por favor, venha novamente.";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close;
	case 2:
		mes .@npc_name$;
		if(Zeny < 5000){
			mes "Sinto muito, mas a taxa de serviço é de 5.000 zenys. Por favor, certifique-se de que você tem dinheiro suficiente para fazer o check-in da próxima vez, ok?";
			close;
		}
		mes "Obrigado.";
		mes "Eu espero que você";
		mes "aproveite seu descanso.";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
