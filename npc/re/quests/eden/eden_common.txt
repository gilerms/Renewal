//===== rAthena Script ======================================= 
//= Eden Group Quests - Common NPCs
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Eden Group Headquarter NPC's.
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Removed showevent use, and use of duplicates.
//=     Commented out warps that should not be active.
//= 1.2 Reduced 300+ copy-paste lines to 1 duplicated NPC. [Brian]
//= 1.3 Some little optimization here and there. [Masao]
//= 1.4 Updated to match the new Izlude Map. [Masao]
//= 1.5 Added some missing Eden Group Teleport Officers. [Masao]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Added Malaya teleporter. [Euphy]
//= 1.8 Updated to match the latest official script. [Euphy]
//= 1.9 Added support to Para_Team_Mark_ in Eden Group. [AtlantisRO/Ragno]
//= 2.0 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

// Common NPCs :: lv_tre
//============================================================
moc_para01,27,35,5	script	Secretária Lime Evenor	4_F_EDEN_OFFICER,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 2000) {
		mes "- Espere um pouco !! -";
		mes "- Você está carregando -";
		mes "- muitos itens no inventário. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de liberar espaço no inventário. -";
		close;
	}
	if (countitem(6219) > 0) {
		mes "[Lime Evenor]";
		mes "Graças aos muitos aventureiros que visitaram nosso Grupo do Éden, recentemente preparamos uma nova versão do Emblema do Éden para nossos visitantes.";
		mes "E também é possível trocar a versão antiga do Emblema do Éden por uma nova.";
		next;
		mes "[Lime Evenor]";
		mes "Embora tenha um pequeno atraso de tempo, esta nova versão do Emblema do Éden o enviará de volta ao local atual salvo.";
		mes "Você gostaria de trocar o seu?";
		next;
		if (select("Claro:Não, obrigado.") == 2){
			mes "[Lime Evenor]";
			mes "Por qual motivo você... não trocaria?";
			close;
		}
		delitem 6219,1;
		getitem 22508,1;
		mes "[Lime Evenor]";
		mes "Ai está.";
		close;
	}
	mes "[Lime Evenor]";
	mes "Pessoas que seguem seus sonhos e romances me ouvem. Somos representantes do paraíso chamado 'O jardim do Éden', chamado grupo do Éden. Não há lugar como este no mundo.";
	mes "Olá. Posso ajudar?";
	next;
	while (1) {
		switch (select("O que é o Grupo do Éden?: Junte-se ao grupo do Éden.:Registre nova missão.:Procura por missões.:Cancelar.")) {
		case 1:
			mes "[Lime Evenor]";
			mes "O Grupo do Éden é um grupo que ajuda pessoas que sofrem de problemas em Rune-Midgard.";
			next;
			mes "[Lime Evenor]";
			mes "Se as pessoas precisarem de ajuda, apresentamos o caso no quadro de avisos e apresentamos aos nossos membros que estão ansiosos para ajudar os necessitados.";
			next;
			mes "[Lime Evenor]";
			mes "Os membros do Éden irão ver as missões e ajudarão aqueles que as publicarem.";
			mes "Os membros fazem essas missões por recompensas.";
			next;
			mes "[Lime Evenor]";
			mes "Se você não está muito familiarizado com Rune-Midgard, esta é uma ótima maneira de explorar o mundo.";
			mes "Ou se você é maduro o suficiente, você pode viajar pelo mundo e fazer novos amigos enquanto estiver fazendo missões. Seu nome honrado será espalhado entre as terras.";
			next;
			break;
		case 2:
			if (!callfunc("F_HasEdenGroupMark")) {
				mes "[Lime Evenor]";
				mes "Você pode ser um membro do Éden simplesmente se registrando comigo.";
				mes "Você gostaria de se juntar ao Grupo do Éden?";
				next;
				switch (select("Sim, quero entrar.:Não, eu não quero entrar.")) {
				case 1:
					mes "[Lime Evenor]";
					mes "Corretamente. Excelente! Por favor, escreva seu nome aqui.";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "Você terminou?";
					mes "Deixe-me ver.";
					next;
					mes "[Lime Evenor]";
					mes "Seu nome é ^3131FFBo"+.@input$+"Ba^000000...? Isso esta certo?";
					mes "Hã? Não está??";
					next;
					emotion ET_SWEAT;
					mes "[Lime Evenor]";
					mes "Hmm, isso não é o que você escreveu?";
					mes "Ok, hmm, parece um pouco difícil de ler.";
					next;
					mes "[Lime Evenor]";
					mes "Você poderia por favor escrever seu nome de novo? Por favor, escreva de forma ^3131FFClara^000000.";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "Ah~ha. Você se chama ^3131FF"+strcharinfo(0)+"^000000.";
					mes "Agora eu consegui entender.";
					next;
					emotion ET_BEST;
					mes "[Lime Evenor]";
					mes "Nós já colocamos seu nome na lista.";
					mes "Bem-vindo ao novo membro do Grupo do Éden!";
					next;
					mes "[Lime Evenor]";
					mes "Espero que você possa fazer um ótimo trabalho como membro do Éden.";
					getitem 22508,1; //Para_Team_Mark_
					next;
					break;
				case 2:
					mes "[Lime Evenor]";
					mes "Você ainda tem dúvidas sobre o Grupo do Éden?";
					next;
					break;
				}
			} else {
				mes "[Lime Evenor]";
				mes "Você já é um membro do Grupo do Éden.";
				next;
			}
			break;
		case 3:
			mes "[Lime Evenor]";
			mes "Você quer registrar algumas missões que você quer que façamos?";
			next;
			if(select("Sim, quero registrar.:Não, obrigado.") == 1) {
				mes "[Lime Evenor]";
				mes "Ok. Por favor, anote seu nome no registro.";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FF"+strcharinfo(0)+"^000000.. Esse é o seu nome?";
				mes "Hmm, sua caligrafia não está clara. Não consigo ler.";
				next;
				mes "[Lime Evenor]";
				mes "^3131FF"+strcharinfo(0)+"^000000, me diga o que você deseja registrar.";
				mes "Para qual mapa devemos ir?";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMapa da Missão: "+.@inputstr$+"^000000";
				mes "hum, vou anotar isso.";
				next;
				mes "[Lime Evenor]";
				mes "Por favor, me diga que tipo de missões você está procurando.";
				mes "Deve ser breve... Algo como 'Caçar 10 Porings.'";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "Você terminou?";
				mes "Deixe-me ver.";
				next;
				mes "[Lime Evenor]";
				mes "Huh...........";
				next;
				mes "[Lime Evenor]";
				mes "Hum.. huh??..................";
				next;
				emotion ET_THINK;
				mes "[Lime Evenor]";
				mes "Acho que você deve completar uma de nossas missões antes de assumir uma nova missão.";
				next;
				emotion ET_HNG;
				mes "[Lime Evenor]";
				mes "^3131FFCliente: Lime Evenor^000000";
				mes "^3131FFMissão: Pratique sua caligrafia por um mês.^000000";
				mes "Você tem uma caligrafia muito ruim!";
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMissão:"+.@input$+"^000000";
				mes "O registro para uma nova missão está feito.";
				next;
				mes "[Lime Evenor]";
				mes "As missões estão lotadas. Você tem que esperar por uma vaga aparecer.";
				mes "Por favor, espere até a sua vez.";
				next;
			}
			break;
		case 4:
			if (callfunc("F_HasEdenGroupMark")) {
				mes "[Lime Evenor]";
				mes "Gostaria de experimentar algumas missões como membro do Éden?";
				next;
				mes "[Lime Evenor]";
				mes "Verifique no lado direito os quadros de avisos, existem vários tipos de missões esperando por você.";
				mes "Por favor, leia atentamente a lista de missões e as qualificações e escolha uma delas.";
				next;
			}
			else {
				mes "[Lime Evenor]";
				mes "Você precisa se juntar ao Grupo do Éden primeiro se quiser fazer algumas missões.";
				close;
			}
			break;
		case 5:
			mes "[Lime Evenor]";
			mes "Se você tiver alguma dúvida, por favor volte novamente.";
			close;
		}
	}
	close;
}

-	script	Grupo do Éden#0::eto	-1,{
	mes "[Grupo do Éden]";
	mes "Aqueles que estão vagando por esse mundo estranho com olhos sem rumo vêm até mim!";
	mes "O Grupo do Éden está aqui para ajudá-lo e lhe mostrará o grande futuro.";
	next;
	mes "[Grupo do Éden]";
	mes "Você não tem nada a fazer senão desperdiçar seu tempo?";
	mes "Você está ansioso para fazer algo de bom, mas ninguém lhe oferece trabalho?";
	mes "Você gostaria de ser um solucionador de problemas?";
	mes "O Grupo do Éden está aqui para resolver seu problema.";
	next;
	switch (select("Teleportar para o Grupo do Éden.:Não quero mais falar.")) {
	case 1:
		mes "[Grupo do Éden]";
		mes "Vamos para a nossa base secreta!";
		set nak_warp,strnpcinfo(2);
		close2;
		warp "moc_para01",31,14;
		end;
	case 2:
		mes "[Grupo do Éden]";
		mes "Se você não sabe o que fazer para o seu futuro, não hesite em me dizer. Apenas passe e fale comigo! Ok?";
		close;
	}
}
prontera,124,76,3	duplicate(eto)	Grupo do Éden#1	729
moc_ruins,68,164,3	duplicate(eto)	Grupo do Éden#2	729
geffen,132,66,3	duplicate(eto)	Grupo do Éden#3	729
alberta,124,67,3	duplicate(eto)	Grupo do Éden#4	729
aldebaran,133,119,5	duplicate(eto)	Grupo do Éden#5	729
izlude_in,68,162,1	duplicate(eto)	Grupo do Éden#6	729
prt_church,103,78,3	duplicate(eto)	Grupo do Éden#7	729
geffen_in,160,104,5	duplicate(eto)	Grupo do Éden#8	729
moc_prydb1,53,126,3	duplicate(eto)	Grupo do Éden#9	729
alberta_in,75,39,3	duplicate(eto)	Grupo do Éden#10	729
payon_in02,58,58,1	duplicate(eto)	Grupo do Éden#11	729
payon,177,111,3	duplicate(eto)	Grupo do Éden#12	729
que_ng,33,73,3	duplicate(eto)	Grupo do Éden#13	729
que_ng,144,161,5	duplicate(eto)	Grupo do Éden#14	729
yuno,144,189,5	duplicate(eto)	Grupo do Éden#15	729
rachel,125,144,3	duplicate(eto)	Grupo do Éden#16	729
comodo,202,151,4	duplicate(eto)	Grupo do Éden#17	729
hugel,93,153,4	duplicate(eto)	Grupo do Éden#18	729
veins,220,109,4	duplicate(eto)	Grupo do Éden#19	729
einbroch,250,211,4	duplicate(eto)	Grupo do Éden#20	729
lighthalzen,164,86,4	duplicate(eto)	Grupo do Éden#21	729
amatsu,100,145,5	duplicate(eto)	Grupo do Éden#22	729
ayothaya,221,191,3	duplicate(eto)	Grupo do Éden#23	729
louyang,224,107,3	duplicate(eto)	Grupo do Éden#24	729
gonryun,162,122,5	duplicate(eto)	Grupo do Éden#25	729
moscovia,209,197,5	duplicate(eto)	Grupo do Éden#26	729
brasilis,191,224,3	duplicate(eto)	Grupo do Éden#27	729
dewata,192,193,5	duplicate(eto)	Grupo do Éden#28	729
morocc,161,97,5	duplicate(eto)	Grupo do Éden#29	729
izlude,131,148,4	duplicate(eto)	Grupo do Éden#30	729
izlude_a,131,148,4	duplicate(eto)	Grupo do Éden#30::eto_iz_a	729
izlude_b,131,148,4	duplicate(eto)	Grupo do Éden#30::eto_iz_b	729
izlude_c,131,148,4	duplicate(eto)	Grupo do Éden#30::eto_iz_c	729
izlude_d,131,148,4	duplicate(eto)	Grupo do Éden#30::eto_iz_d	729
umbala,105,158,3	duplicate(eto)	Grupo do Éden#31	729
malaya,238,206,6	duplicate(eto)	Grupo do Éden#32	729
verus04,116,243,0	duplicate(eto)	Grupo do Éden#33	729

moc_para01,30,10,0	script	#eden_out	45,1,1,{
OnTouch:
	switch (nak_warp) {
		case 1: warp "prontera",116,72; end;
		case 2: warp "moc_ruins",64,161; end;
		case 3: warp "geffen",120,39; end;
		case 4: warp "alberta",117,56; end;
		case 5: warp "aldebaran",168,112; end;
		case 6: warp "izlude_in",73,165; end;
		case 7: warp "prt_church",99,78; end;
		case 8: warp "geffen_in",162,99; end;
		case 9: warp "moc_prydb1",51,118; end;
		case 10: warp "alberta_in",73,43; end;
		case 11: warp "payon_in02",64,60; end;
		case 12: warp "payon",161,58; end;
		case 13: warp "que_ng",33,63; end;
		case 14: warp "que_ng",144,166; end;
		case 15: warp "yuno",158,125; end;
		case 16: warp "rachel",115,125; end;
		case 17: warp "comodo",192,145; end;
		case 18: warp "hugel",88,148; end;
		case 19: warp "veins",216,104; end;
		case 20: warp "einbroch",246,204; end;
		case 21: warp "lighthalzen",159,95; end;
		case 22: warp "amatsu",110,150; end;
		case 23: warp "ayothaya",217,178; end;
		case 24: warp "louyang",217,103; end;
		case 25: warp "gonryun",155,120; end;
		case 26: warp "moscovia",218,198; end;
		case 27: warp "brasilis",190,220; end;
		case 28: warp "dewata",192,182; end;
		case 29: warp "morocc",161,97; end;
		case 30: warp "izlude",134,118; end;	// Old coordinates: (127,142)
		case 31: warp "umbala",94,154; end;
		case 32: warp "malaya",234,199; end;
		case 33: warp "verus04",115,243; end;
		default: warp "prontera",116,72; end;
	}
	end;
}

moc_para01,47,39,3	script	#warp_2_pass	111,{
	if (callfunc("F_HasEdenGroupMark")) {
		mes "[Lime Evenor]";
		mes "Oh, aqui é um lugar exclusivo apenas para os membros do Éden.";
		mes "Se você é um membro, você pode vir quando quiser!";
		next;
		switch (select("Entrar.:Não entrar.")) {
		case 1:
			mes "Esta porta é lindamente decorada, mas parece um pouco pesada.";
			close2;
			warp "moc_para01",106,14;
			end;
		case 2:
			mes "[Lime Evenor]";
			mes "Bem... Se você não está interessado.";
			close;
		}
	}
	mes "[Lime Evenor]";
	mes "Oh, aqui é um lugar exclusivo apenas para os membros do Éden.";
	mes "Se você quiser entrar, você precisa se juntar ao Grupo do Éden.";
	close;
}

moc_para01,16,22,7	script	Velho Aventureiro#eden	900,{
	mes "[Velho Aventureiro]";
	mes "Ah!! Eu sou forte o suficiente, mesmo que eu pareça velho! Qual é o nome dele? Lime Evenor? Ele apenas me ignora como se eu fosse uma espécie de velho bastardo senil.";
	next;
	mes "[Velho Aventureiro]";
	mes "Eu vim aqui para se juntar ao grupo Eden! Eu não mereço isso!";
	mes "Você também acha que eu pareço ser senil?";
	close;
}

moc_para01,182,48,3	script	Chef do Éden#eden	886,{
	set .@eggf,rand(1,118);
	set .@eggf_1,.@eggf + 1;
	set .@eggf_2,.@eggf + 2;
	set .@eggf_6,.@eggf + 6;
	mes "[Chef do Éden]";
	mes ""+.@eggf+".. "+.@eggf_1+"... "+.@eggf_2+".... "+.@eggf_6+"..?";
	next;
	emotion ET_CRY;
	mes "[Chef do Éden]";
	mes "Oh! Quantos ovos eu fiz?";
	mes "Aww!!!Eu esqueci totalmente! Eu tenho que contar de novo!";
	next;
	emotion ET_CRY;
	mes "[Chef do Éden]";
	mes "Por que você me pediu para fazer isso? Que tipo de missão é essa?";
	next;
	emotion ET_CRY;
	mes "[Chef do Éden]";
	mes "O que você vai com todos esses ovos fritos? Tenho certeza de que você não pode comer todos eles. Você só quer me incomodar, certo?";
	close;
}

moc_para01,172,28,5	script	Estagiário do Éden#eden	883,{
	mes "[Estagiário do Éden]";
	mes "Na verdade, percebi isso.";
	next;
	mes "[Estagiário do Éden]";
	mes "Temos que trabalhar muito, a menos que você queira falhar.";
	next;
	mes "[Estagiário do Éden]";
	mes "Mas eu nunca aprendi sobre produtos farmacêuticos ainda.";
	next;
	mes "[Estagiário do Éden]";
	mes "Eu vou falhar. Eu não posso fazer isso.";
	close;
}

moc_para01,20,35,5	script	Assistente de Escritório Neede	814,{
	mes "[Neede]";
	mes "Eu me tornei membro do Éden há vários anos, mas parece que foi ontem.";
	mes "Eu tenho feito muito trabalho, então nem percebi quantos anos passei aqui.";
	next;
	mes "[Neede]";
	mes "Isso me lembra que era como uma guerra todos os dias.";
	next;
	mes "[Neede]";
	mes "O chef do ^3131FFrestaurante do lado direito^000000";
	mes "usa muito tempero toda vez que cozinha, o cheiro está em todo o lobby. Temos que abrir a janela regularmente porque não aguentamos o cheiro!";
	next;
	mes "[Neede]";
	mes "E você vê o ^3131FFGinásio^000000 na cruz ^3131FFdo lado norte do lobby^000000? Eles sempre gritam quando se exercitam, eu até posso ouvi-los daqui!";
	next;
	emotion ET_ANGER;
	mes "[Neede]";
	mes "Além disso, a garota estranha ^3131FFdo segundo andar^000000 está chorando o tempo todo. Seus gritos me deixam louca.";
	next;
	mes "[Neede]";
	mes "Ah, aquela garota esquisita é nossa chefe...";
	next;
	mes "[Neede]";
	mes "Eu não sei o que ela tem feito. Mas ela chora e grita o tempo todo. Oh, às vezes eu ouço 'Bang!', Sons altos e barulhentos ... não sei de onde veio.";
	next;
	emotion ET_HUK;
	mes "[Neede]";
	mes "Espere!";
	mes "Shh! É um segredo, você não pode contar a ninguém!";
	next;
	mes "[Neede]";
	emotion ET_OHNO;
	mes "Eu não confio em Evenor. Ela nunca mostra seus sentimentos ... ela vai me entregar ao meu chefe.";
	next;
	emotion ET_HNG, getnpcid(0, "Secretária Lime Evenor");
	mes "[Lime Evenor]";
	mes "Eu não sou esse tipo de cara.";
	next;
	emotion ET_KEK;
	mes "[Neede]";
	mes "Hã? Você ouviu isso?";
	close;
}

function	script	F_HasEdenGroupMark	{
	return (countitem(6219) > 0 || countitem(22508) > 0);
}
