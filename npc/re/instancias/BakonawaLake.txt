//===== rAthena Script ======================================= 
//= Bakonawa Lake
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Defeat Bakonawa before he eats the moon.
//= Part of the "Bakonawa Extermination" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added GM management NPC. [Euphy]
//= 1.2 Added VIP features. [Euphy]
//============================================================ 

// Instance Creation :: ma_baku
//============================================================
ma_scene01,174,179,4	script	Taho	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "Você está carregando muitos itens. Volte aqui após de limpar seu inventário.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Você está muito pesado. Volte aqui após reduzir o peso.";
		close;
	}
	if (BaseLevel < 140) {
		mes "[Taho]";
		mes "Ei. Esse lugar é muito perigoso para você. Você não pode se manter seguro. Saia daqui e vá para uma cidade agora mesmo!";
		close;
	}
	if (malaya_bakona1 < 7 && malaya_bakona2 < 15) {
		mes "[Taho]";
		mes "Vamos ser mortos por Bakonawa. Ah, devemos matá-lo, mas infelizmente não estamos preparados.";
		close;
	}

	set .@party_id,getcharid(1);
	set .@md_name$, "Bakonawa Lake";

	set .@baku_time, checkquest(12278,PLAYTIME);
	if (.@baku_time == -1) {
		if (checkquest(12279) == -1) setquest 12279;
	}
	if (!.@party_id) {
		mes "[Taho]";
		mes "Não há ninguém que possa ir com você? Você pode ir sozinho, mas venha deopis de fazer um grupo.";
		close;
	}
	if (.@baku_time == -1) {
		if (getcharid(0) == getpartyleader(.@party_id,2)) {
			mes "[Taho]";
			mes "Você é o líder dos assassinos de Bakonawa? A estrada não está boa, então temos que descer o penhasco com uma corda, está bem?";
			next;
			set .@i, select("Por favor tecer uma corda.:Agora vou descer.:Cancelar.");
		} else {
			mes "[Taho]";
			mes "Você veio para matar Bakonawa. O líder do seu grupo tem que me ajudar a tecer a corda para descer.";
			next;
			set .@i, select(":Agora vou descer.:Cancelar.");
		}
		switch(.@i) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[Taho]";
				mes "Ah, a corda caiu. Eu tenho que fazer uma nova.";
				close;
			}
			mes "[Taho]";
			mes "Agora estou tecendo, então você pode descer quando eu terminar.";
			close;
		case 2:
			switch(instance_enter(.@md_name$)) {
			case IE_OTHER:
				mes "[Taho]";
				mes "Ocorreu um erro desconhecido.";
				close;
			case IE_NOINSTANCE:
				mes "[Taho]";
				mes "A corda ainda não foi tecida. Espere um pouco.";
				close;
			case IE_NOMEMBER:
				mes "[Taho]";
				mes "Os civis não podem entrar.";
				close;
			case IE_OK:
				mapannounce "ma_scene01","O membro, "+strcharinfo(0)+" do grupo "+getpartyname(.@party_id)+" está entrando na masmorra, "+.@md_name$+".",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
				setquest 12278;
				//warp "1@ma_b",64,51;
				close;
			}
		case 3:
			close;
		}
	} else if (.@baku_time == 0 || .@baku_time == 1) {
		mes "[Taho]";
		mes "A corda usada para chegar ao lago já está quebrada. Agora estamos limpando o ambiente, então volte depois de algum tempo.";
		close;
	} else if (.@baku_time == 2) {
		mes "[Taho]";
		mes "Está tudo limpo por aqui. Agora você pode descer novamente depois de pegar uma corda.";
		erasequest 12278;
		close;
	}
}

sec_in02,26,26,4	script	Bakonawa's Rage	541,{
	callfunc "F_GM_NPC";
	mes "[Taho]";
	mes "My name is Minky.";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		erasequest 12278;
		erasequest 12279;
		set malaya_bakona1,7;
		set malaya_bakona2,15;
	}
	close;
}

// Instance Scripts :: in_baku01
//============================================================
1@ma_b,62,52,4	script	Taho#nf	541,{
	mes "[Taho]";
	mes "Hahah, eu poderia me juntar à caçada de Bakonawa, mas eu prefiro me esconder atrás daquela pedra e torcer por vocês.";
	next;
	mes "[Taho]";
	mes "Bakonawa se torna invencível em algum momento. Quando chegar a hora, ouça atentamente meus guias. Apenas confie em mim e tudo ficará bem.";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[Taho]";
		mes "Agora vou fazer Bakonawa saltar do lago.";
		next;
		switch(select("Espere! Eu não estou pronto ainda!:Vamos lá!")) {
		case 1:
			mes "[Taho]";
			mes "Ah ... tudo bem ... me diga quando estiver pronto.";
			close;
		case 2:
			mes "[Taho]";
			mes "Então deixarei cair uma gota de Albopal neste lago.";		// Albopal?
			next;
			mes "[Taho]";
			mes "Depois que Bakonawa provar isso, ele irá aparecer ...";
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			disablenpc instance_npcname("Taho#nf");
			close;
		}
	} else {
		mes "[Taho]";
		mes "Eu estou largando o Alpopal para deixar Bakonawa louco. Bakonawa pode ser uma boa fonte de proteína quando o pegamos.";
		close;
	}
}

1@ma_b,36,111,4	script	#Bakonawan1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan1");
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Taho: Eu posso vê-lo lá na superfície! ATAQUE!!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	monster .@map$,78,83,"Bakonawa",2320,1,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	if (mobcount(.@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#BakonawaDeadn1")+"::OnStart";
		killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan1");
	}
	end;
OnTimer1000:	callsub OnAnnounce,"10 minutos";
OnTimer60000:	callsub OnAnnounce,"9 minutos";
OnTimer120000:	callsub OnAnnounce,"8 minutos";
OnTimer180000:	callsub OnAnnounce,"7 minutos";
OnTimer240000:	callsub OnAnnounce,"6 minutos";
OnTimer300000:	callsub OnAnnounce,"5 minutos";
OnTimer360000:	callsub OnAnnounce,"4 minutos";
OnTimer420000:	callsub OnAnnounce,"3 minutos";
OnTimer480000:	callsub OnAnnounce,"2 minutos";
OnTimer540000:	callsub OnAnnounce,"1 minuto";
OnTimer570000:
	mapannounce instance_mapname("1@ma_b"),"Tempo limite: 30 segundos",bc_map,"0xff4400"; //FW_NORMAL 18 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa escapou para o fundo do lago.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Tempo limite: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,78,81,0	script	#SummonPuppetsn1	139,4,5,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#SummonPuppetsn1");
	initnpctimer;
	end;
OnTouch:
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnTimer5000:
	setarray .@x[0],79,71,60,61,57,89,95,96,99;
	setarray .@y[0],71,72,80,90,99,73,82,90,99;
	set .@map$, instance_mapname("1@ma_b");
	for(set .@i,0; .@i<9; set .@i,.@i+1) {
		set .@rand, rand(1,10);
		if (.@rand > 7)
			monster .@map$,.@x[.@i],.@y[.@i],"Bakonawa's Will",2337,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		else if (.@rand < 4)
			monster .@map$,.@x[.@i],.@y[.@i],"Bakonawa's Will",2343,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	}
	end;
OnTimer50000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,1,5,4	script	#BakonawaDeadn1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
OnStart:
	enablenpc instance_npcname("#BakonawaDeadn1");
	initnpctimer;
	end;
OnTimer100:
	mapannounce instance_mapname("1@ma_b"),"Taho: Ótimo! Bakonawa está escondido debaixo d'água. Ele agora tentará engolir a lua.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Devemos fazer barulhos altos com gongs e caldeirão para que ele nunca possa se concentrar na lua!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Não se envolva com ele, mesmo que ele venha acima da superfície. Apenas faça barulhos altos!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@ma_b"),"Missão - Destrua 2 caldeiros e 2 gongos nos lados esquerdo e direito do lago.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan2")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2");
	set .@label$, instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	monster .@map$,95,98,"Caldron",2328,1,.@label$;
	monster .@map$,60,98,"Caldron",2328,1,.@label$;
	monster .@map$,97,104,"Gong",2328,1,.@label$;
	monster .@map$,58,104,"Gong",2328,1,.@label$;
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnStart";
	initnpctimer;
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnEnd";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2");
	} else
		mapannounce .@map$,"Taho: Good! "+.@mob_dead_num+" deixou de ir!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	 end;
OnTimer1000:	callsub OnAnnounce,"5 minutos",15,1;
OnTimer60000:	callsub OnAnnounce,"4 minutos",15,1;
OnTimer120000:	callsub OnAnnounce,"3 minutos",15,1;
OnTimer180000:	callsub OnAnnounce,"2 minutos",15,1;
OnTimer240000:	callsub OnAnnounce,"1 minuto",15,1;
OnTimer270000:	callsub OnAnnounce,"30 segundos",15,0;
OnTimer280000:	callsub OnAnnounce,"20 segundos",15,0;
OnTimer290000:	callsub OnAnnounce,"10 segundos",15,0;
OnTimer295000:	callsub OnAnnounce,"5 segundos",16,0;
OnTimer296000:	callsub OnAnnounce,"4 segundos",17,0;
OnTimer297000:	callsub OnAnnounce,"3 segundos",18,0;
OnTimer298000:	callsub OnAnnounce,"2 segundos",19,0;
OnTimer299000:	callsub OnAnnounce,"1 segundo",20,0;
OnTimer300000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa escapou para o fundo do lago.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Tempo limite: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2-1");
	monster instance_mapname("1@ma_b"),78,93,"Enraged Bakonawa",2321,1,instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnEnd:
	enablenpc instance_npcname("#Bakonawan2-1");
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnTimer1000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Ele está de volta ao lago novamente. Está acabado?",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"Taho: UHM! Algo está se movendo rapidamente sob a água! Ba ... Bakonawa está chegando !!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Missão - Elimine Bakonawa que se tornou mais feroz.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan3")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan3");
	initnpctimer;
	monster instance_mapname("1@ma_b"),78,83,"Enraged Bakonawa",2322,1,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	donpcevent instance_npcname("#Bakonawan3-1")+"::OnStart";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		mapannounce .@map$,"Taho: Nós o pegamos! Eu posso ver que ele vomitou algo que parece uma caixa.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("Taho#Completed")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan3-1")+"::OnEnd";
		killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan3");
	}
	end;
OnTimer1000:
	callsub OnAnnounce,"10 minutos",15,1;
	end;
OnTimer60000:
OnTimer120000:
OnTimer180000:
OnTimer240000:
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer300000:	callsub OnAnnounce,"5 minutos",15,1;
OnTimer360000:	callsub OnAnnounce,"4 minutos",15,1;
OnTimer420000:	callsub OnAnnounce,"3 minutos",15,1;
OnTimer480000:	callsub OnAnnounce,"2 minutos",15,1;
OnTimer540000:	callsub OnAnnounce,"1 minuto",15,1;
OnTimer570000:	callsub OnAnnounce,"30 segundos",15,0;
OnTimer580000:	callsub OnAnnounce,"20 segundos",15,0;
OnTimer590000:	callsub OnAnnounce,"10 segundos",15,0;
OnTimer595000:	callsub OnAnnounce,"5 segundos",16,0;
OnTimer596000:	callsub OnAnnounce,"4 segundos",17,0;
OnTimer597000:	callsub OnAnnounce,"3 segundos",18,0;
OnTimer598000:	callsub OnAnnounce,"2 segundos",19,0;
OnTimer599000:	callsub OnAnnounce,"1 segundo",20,0;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa escapou para o fundo do lago.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Tempo limite: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3-1");
	end;
OnStart:
	initnpctimer;
	end;
OnEnd:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	//showdigit 0;
	stopnpctimer;
	end;
OnMyMobDead:
	showdigit mobcount(instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead");
	end;
OnTimer120000:	callsub OnMobSpawn,10;
OnTimer180000:	callsub OnMobSpawn,15;
OnTimer240000:	callsub OnMobSpawn,20;
//OnTimer300000:	callsub OnMobSpawn,25;
OnTimer300000:	callsub OnMobSpawn,30;
OnTimer360000:	callsub OnMobSpawn,35;
OnTimer420000:	callsub OnMobSpawn,40;
OnTimer480000:	callsub OnMobSpawn,45;
OnTimer540000:	callsub OnMobSpawn,50;
OnTimer600000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	stopnpctimer;
	end;
OnMobSpawn:
	set .@label$, instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	killmonster .@map$,.@label$;
	set .@mob_dead_num, mobcount(.@map$,.@label$);
	if (.@mob_dead_num < 50) {
		set .@mob_rg, 50 - .@mob_dead_num;
		if (.@mob_rg > getarg(0))
			set .@mob_rg, getarg(0);
		while(1) {
			areamonster .@map$,74,74,82,74,"Bakonawa's Puppet",2334,1,.@label$;
			set .@mob_dead_num, mobcount(.@map$,.@label$);
			if (.@mob_dead_num >= .@mob_rg) {
				break;
			}
		}
	}
	end;
}

1@ma_b,62,52,4	script	Taho#Completed	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "Você está carregando muitos itens. Volte aqui após de limpar seu inventário.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Você está muito pesado. Volte aqui após reduzir o peso.";
		close;
              }
	if (checkquest(12279,HUNTING) == 2) {
		erasequest 12279;
		mes "[Taho]";
		mes "Isso foi encontrado no cadáver de Bakonawa. Seria melhor se você pegasse isso.";
		if (!VIP_SCRIPT || vip_status(VIP_STATUS_ACTIVE))
			getitem 6499,7; //Ancient_Grudge
		else
			getitem 6499,5; //Ancient_Grudge
		getexp 0,100000;
		next;
	}
	mes "[Taho]";
	mes "É melhor nos apressarmos e sairmos desse lugar. Este lugar ainda é perigoso mesmo sem Bakonawa...";
	next;
	mes "[Taho]";
	mes "Então vamos usar a corda para subir!";
	close2;
	warp "ma_scene01",175,176;
	end;
OnInstanceInit:
	disablenpc instance_npcname("Taho#Completed");
	end;
OnStart:
	enablenpc instance_npcname("Taho#Completed");
	initnpctimer;
	end;
OnTimer1000:
	monster instance_mapname("1@ma_b"),78,74,"Bakonawa Treasure Box",2335,1;
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Taho: Depois de abrir a caixa do tesouro, por favor, visite-me no topo da colina. Eu tenho algo para te dar.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	stopnpctimer;
	end;
}

1@ma_b,62,52,4	script	Taho#Fail	541,{
	mes "[Taho]";
	mes "Hmm. Nós quase o pegamos, mas ele é uma criatura rápida...";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[Taho]";
		mes "Nós vamos tentar de novo?";
		next;
		switch(select("Espere um segundo!:Claro! Nós não podemos nos afastar agora!")) {
		case 1:
			mes "[Taho]";
			mes "AWW ... quase o levei para fora.";
			close;
		case 2:
			mes "[Taho]";
			mes "Então eu vou usar o Albopal mais uma vez no lago.";
			next;
			mes "[Taho]";
			mes "Estamos prestes a ver Bakonawa com raiva intensa...";
			disablenpc instance_npcname("Taho#Fail");
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			close;
		}
		close;
	} else {
		mes "[Taho]";
		mes "Eu estou soltando Albopal como eu fiz há um tempo atrás. Eu espero que isso consiga irritá-lo...";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Taho#Fail");
	end;
OnStart:
	enablenpc instance_npcname("Taho#Fail");
	end;
}
