//===== rAthena Script =======================================
//= Milk Trader
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Trades bottles for milk
//===== Additional Comments: =================================
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Raised the price to close zeny exploit [Lupus]
//= 1.2a Switched to Lupus's "loopless" technique.[kobra_k88]
//= 1.3 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4 Implemented checkweight. [L0ne_W0lf]
//= 1.5 Fixed missed variable. (bugreport:1523) [L0ne_W0lf]
//============================================================

prontera,73,140,0	script	Leiteiro	86,{
	if (checkweight(1201,1) == 0) {
		mes "^3355FFApenas um minuto!";
		mes "Eu não posso oferecer nada do meu";
		mes "presta serviços para você porque";
		mes "você está carregando muito";
		mes "coisas. Coloque seus itens extras em";
		mes "Armazenamento Kafra e volte novamente~";
		close;
	}
	mes "[Vendedor de Leite]";
	mes "Ei, ei ...";
	mes "Se você me trouxer";
	mes "1 garrafa vazia e";
	mes "15 Zeny, eu vou trocar";
	mes "eles por 1 leite. Como";
	mes "isso soa?";
	next;
	if (select("Troque todas as garrafas vazias.:Cancelar") == 1) {
		if (countitem(713) <= 0) {
			mes "[Vendedor de Leite]";
			mes "Ei ...";
			mes "Você não tem";
			mes "qualquer garrafa vazia.";
			mes "Eu não posso te dar";
			mes "este leite qualquer outro";
			mes "você sabe ...";
			close;
		}
		set .@bottles,countitem(713);
		set .@total_weight,.@bottles * 50;
		set .@total_cost,.@bottles * 15;
		if (Zeny < .@Total_cost) {
			mes "[Vendedor de Leite]";
			mes "Oh, whoa ~!";
			mes "Você não tem o suficiente";
			mes "zeny para trocar tudo";
			mes "essas garrafas vazias para";
			mes "Leite. Você precisa ter";
			mes "pelo menos " + .@total_cost + " zeny.";
			close;
		}
		if (MaxWeight - Weight < .@total_weight) {
			mes "[Vendedor de Leite]";
			mes "Hmm ...";
			mes "Você faria";
			mes "um pouco mais de espaço";
			mes "no seu inventário";
			mes "antes de te dar";
			mes "todo esse leite?";
			close;
		}
		set Zeny, Zeny-.@total_cost;
		delitem 713,.@bottles; //Empty Bottles
		getitem 519,.@bottles; //Milk
		close;
	}
	close;
}
